sourceSets.main.java.srcDirs = [ "src/" ]
sourceCompatibility = '1.7'
[compileJava, compileTestJava]*.options*.encoding = 'UTF-8'
ext.mainClassName = "non.ios.IOSLauncher"

task copyNatives << {
    file("build/libs/ios/").mkdirs();
    configurations.natives.files.each { jar ->
        def outputDir = null
        if (jar.name.endsWith("natives-ios.jar")) outputDir = file("build/libs/ios")
        if (outputDir != null) {
            copy {
                from zipTree(jar)
                into outputDir
                include "*.a"
            }
        }
    }
}

task updateRoboVMXML(dependsOn: copyNatives) << {
    def xml = file('robovm.xml')
    if (!xml.exists()) { return }
  
    def libtree = fileTree(dir: 'build/libs', include: '**/*.a')
    def config = new groovy.util.XmlParser().parse(xml)
    config.libs.each {libs ->
        libs.children().clear()
        libtree.each { File file ->
            libs.appendNode('lib', 'build/libs/ios/' + file.getName())
        }
    }
  
    def writer = new FileWriter(xml)
    def printer = new XmlNodePrinter(new PrintWriter(writer))
    printer.setPreserveWhitespace true
    printer.print(config)
}

task run(dependsOn: launchIPhoneSimulator)

task update(dependsOn: ':update') << {
    copy {
        file("data").mkdirs()
        from "../../project/res"
        into "data"
        include "icon-57.png"
        rename "icon-57.png", "Icon.png"
        include "icon-72.png"
        rename "icon-72.png", "Icon-72.png"
        include "icon-114.png"
        rename "icon-114.png", "Icon@2x.png"
        include "icon-144.png"
        rename "icon-144.png", "Icon-72@2x.png"
        include "splash-320x480.png"
        rename "splash-320x480.png", "Default.png"
        include "splash-640x960.png"
        rename "splash-640x960.png", "Default@2x.png"
        include "splash-640x1136.png"
        rename "splash-640x1136.png", "Default-568h@2x.png"
        include "splash-750x1334.png"
        rename "splash-750x1334.png", "Default-375w-667h@2x.png"
        include "splash-768x1004.png"
        rename "splash-768x1004.png", "Default~ipad.png"
        include "splash-1242x2208.png"
        rename "splash-1242x2208.png", "Default-414w-736h@3x.png"
        include "splash-1536x2008.png"
        rename "splash-1536x2008.png", "Default@2x~ipad.png"
    }
}

task dist(dependsOn: [update, createIPA]) << {
    println "You can find ${appName}-${version}.ipa in /dist/ios"
}

build.dependsOn updateRoboVMXML
launchIPhoneSimulator.dependsOn build
launchIPadSimulator.dependsOn build
launchIOSDevice.dependsOn build
createIPA.dependsOn build